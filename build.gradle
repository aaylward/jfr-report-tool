plugins {
    id 'groovy'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    compile 'commons-cli:commons-cli:1.3.1'
    compile fileTree(
            dir: new File(System.getenv('JAVA_HOME'), 'lib/missioncontrol/plugins'),
            includes: ['com.jrockit.mc.common_*.jar', 'com.jrockit.mc.flightrecorder_*.jar']
    )
    compile 'com.google.guava:guava:18.0'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'junit:junit:4.12'
}

mainClassName = "io.github.lhotari.JfrReportTool"

run {
    if (project.hasProperty('runArgs')) {
        args = project.getProperty('runArgs').split(',') as List
    }
}